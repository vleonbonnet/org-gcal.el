;;; org-gcal-test.el --- Tests for org-gcal.el -*- lexical-binding: t -*-

;; Copyright (C) 2019 Robert Irelan
;; Package-Requires: ((org-gcal) (el-mock) (emacs "26") (load-relative "1.3"))

;; Author: Robert Irelan <rirelan@gmail.com>

;; This file is not part of GNU Emacs.

;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:

;; Tests for org-gcal.el

;;; Code:

;; Must set these variables before loading the package, but don’t reset them if
;; they’re already set.
(unless (and (boundp 'org-gcal-client-id) org-gcal-client-id
             (boundp 'org-gcal-client-secret) org-gcal-client-secret)
  (setq org-gcal-client-id "test_client_id"
        org-gcal-client-secret "test_client_secret"))

(require 'org-gcal)
(require 'cl-lib)
(require 'el-mock)
(require 'load-relative)
(unless (featurep 'org-test)
  (load-relative "org-test"))

(defconst org-gcal-test-calendar-id "foo@foobar.com")

(defconst org-gcal-test-event-json
  "\
{
 \"kind\": \"calendar#event\",
 \"etag\": \"\\\"12344321\\\"\",
 \"id\": \"foobar1234\",
 \"status\": \"confirmed\",
 \"htmlLink\": \"https://www.google.com/calendar/event?eid=foobareid1234\",
 \"hangoutsLink\": \"https://hangouts.google.com/my-meeting-id\",
 \"location\": \"Foobar's desk\",
 \"transparency\": \"opaque\",
 \"created\": \"2019-09-27T20:50:45.000Z\",
 \"updated\": \"2019-10-06T22:59:47.287Z\",
 \"summary\": \"My event summary\",
 \"description\": \"My event description\\n\\nSecond paragraph\",
 \"creator\": {
  \"email\": \"foo@foobar.com\",
  \"displayName\": \"Foo Foobar\"
 },
 \"organizer\": {
  \"email\": \"bar@foobar.com\",
  \"self\": true
 },
 \"start\": {
  \"dateTime\": \"2019-10-06T17:00:00-07:00\"
 },
 \"end\": {
  \"dateTime\": \"2019-10-06T21:00:00-07:00\"
 },
 \"reminders\": {
  \"useDefault\": true
 },
 \"source\": {
  \"url\": \"https://google.com\",
  \"title\": \"Google\"
 }
}
")

(defconst org-gcal-test-html-event-json
  (replace-regexp-in-string
   "My event description\\\\n\\\\nSecond paragraph"
   "<html-blob>Click the link:<br><a href=\\\\\"https://meet.jit.si/Weekly\\\\\">https://meet.jit.si/Weekly</a><br><br>Items:<br><ul><li>First</li><li>Second</li></ul>&amp; more info</html-blob>"
   org-gcal-test-event-json))

(defconst org-gcal-test-cancelled-event-json
  (replace-regexp-in-string "confirmed" "cancelled"
                            org-gcal-test-event-json))

(defconst org-gcal-test-full-day-event-json
  (replace-regexp-in-string
   "\"dateTime\": \"2019-10-06T17:00:00-07:00\""
   "\"date\": \"2019-10-06\""
   (replace-regexp-in-string
    "\"dateTime\": \"2019-10-06T21:00:00-07:00\""
    "\"date\": \"2019-10-07\""
    org-gcal-test-event-json)))

(defmacro org-gcal-test--with-temp-buffer (contents &rest body)
  "Create a ‘org-mode’ enabled temp buffer with CONTENTS.
BODY is code to be executed within the temp buffer.  Point is
always located at the beginning of the buffer."
  (declare (indent 1) (debug t))
  `(with-temp-buffer
     (org-mode)
     (insert ,contents)
     (goto-char (point-min))
     ,@body))

(defun org-gcal-test--stale-marker (contents)
  "Return a marker whose buffer was killed after inserting CONTENTS."
  (let ((buffer (generate-new-buffer " *org-gcal-stale-marker*")))
    (with-current-buffer buffer
      (org-mode)
      (insert contents)
      (goto-char (point-min))
      (prog1 (point-marker)
        (kill-buffer buffer)))))

(defmacro org-gcal-test--should-error-match (regexp &rest body)
  "Assert that BODY signals an error whose message matches REGEXP."
  (declare (indent 1) (debug t))
  `(let ((err (should-error (progn ,@body) :type 'error)))
     (should (string-match-p ,regexp (error-message-string err)))))

(defun org-gcal-test--json-read-string (json)
  "Wrap ‘org-gcal--json-read’ to parse a JSON string."
  (with-temp-buffer
    (insert json)
    (org-gcal--json-read)))

(defun org-gcal-test--title-to-string (elem)
  "Get :title from ELEM and convert to string."
  (let ((prop (org-element-property :title elem)))
    (cond
     ((listp prop) (car prop))
     ((stringp prop) prop)
     (t
      (user-error "org-gcal-test--title-to-string: unhandled type for :title prop of elem %S"
                  elem)))))

(defconst org-gcal-test-event
  (org-gcal-test--json-read-string org-gcal-test-event-json))

(defconst org-gcal-test-html-event
  (org-gcal-test--json-read-string org-gcal-test-html-event-json))

(defconst org-gcal-test-cancelled-event
  (org-gcal-test--json-read-string org-gcal-test-cancelled-event-json))

(defconst org-gcal-test-full-day-event
  (org-gcal-test--json-read-string org-gcal-test-full-day-event-json))

(ert-deftest org-gcal-test--save-sexp ()
  "Verify that org-gcal--save-sexp saves its data to the right place."
  (let* ((file
          ;; When I rescan ID locations, the symlink "/var" is resolved to
          ;; "/private/var" on macOS. The simplest way to fix this is just to
          ;; resolve the symlink manually at the start.
          ;;
          ;; Also need to run ‘abbreviate-file-name’ in case the temp file is
          ;; created under HOME.
          (abbreviate-file-name (file-truename (make-temp-file "org-gcal-test--save-sexp.")))))
    (unwind-protect
        (org-gcal-test--with-temp-buffer
         ""
         (let ((data '(:foo :bar)))
           (org-gcal--save-sexp data file)
           (should (string-equal (buffer-string)
                                 ""))
           (should (equal (org-gcal--read-file-contents file)
                          `(:token ,data :elem nil)))
           (setq data '(:baz :quux))
           (org-gcal--save-sexp data file)
           (should (equal (buffer-string)
                          ""))
           (should (equal (org-gcal--read-file-contents file)
                          `(:token ,data :elem nil))))))))

(ert-deftest org-gcal-test--update-empty-entry ()
  "Verify that an empty headline is populated correctly from a calendar event
object."
  (org-gcal-test--with-temp-buffer
      "* "
    (org-gcal--update-entry org-gcal-test-calendar-id
                            org-gcal-test-event)
    (org-back-to-heading)
    (let ((elem (org-element-at-point)))
      (should (equal (org-gcal-test--title-to-string elem)
                     "My event summary"))
      (should (equal (org-element-property :ETAG elem)
                     "\"12344321\""))
      (should (equal (org-element-property :LOCATION elem)
                     "Foobar's desk"))
      (should (equal (org-element-property :LINK elem)
                     "[[https://google.com][Google]]"))
      (should (equal (org-element-property :TRANSPARENCY elem)
                     "opaque"))
      (should (equal (org-element-property :CALENDAR-ID elem)
                     "foo@foobar.com"))
      (should (equal (org-element-property :ENTRY-ID elem)
                     "foobar1234/foo@foobar.com")))
    ;; Check contents of "org-gcal" drawer
    (re-search-forward ":org-gcal:")
    (let ((elem (org-element-at-point)))
      (should (equal (org-element-property :drawer-name elem)
                     "org-gcal"))
      (should (equal (buffer-substring-no-properties
                      (org-element-property :contents-begin elem)
                      (org-element-property :contents-end elem))
                     "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))))

(ert-deftest org-gcal-test--update-existing-entry ()
  "Verify that an existing headline is populated correctly from a calendar event
object."
  (org-gcal-test--with-temp-buffer
   "\
* Old event summary
:PROPERTIES:
:ETag:     \"9999\"
:LOCATION: Somewhere else
:link: [[https://yahoo.com][Yahoo!]]
:TRANSPARENCY: transparent
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<9999-10-06 Sun 17:00-21:00>

Old event description
:END:
"
   (org-gcal--update-entry org-gcal-test-calendar-id
                           org-gcal-test-event)
   (org-back-to-heading)
   (let ((elem (org-element-at-point)))
     (should (equal (org-gcal-test--title-to-string elem)
                    "My event summary"))
     (should (equal (org-element-property :ETAG elem)
                    "\"12344321\""))
     (should (equal (org-element-property :LOCATION elem)
                    "Foobar's desk"))
     (should (equal (org-element-property :LINK elem)
                    "[[https://google.com][Google]]"))
     (should (equal (org-element-property :TRANSPARENCY elem)
                    "opaque"))
     (should (equal (org-element-property :CALENDAR-ID elem)
                    "foo@foobar.com"))
     (should (equal (org-element-property :ENTRY-ID elem)
                    "foobar1234/foo@foobar.com")))
   ;; Check contents of "org-gcal" drawer
   (re-search-forward ":org-gcal:")
   (let ((elem (org-element-at-point)))
     (should (equal (org-element-property :drawer-name elem)
                    "org-gcal"))
     (should (equal (buffer-substring-no-properties
                     (org-element-property :contents-begin elem)
                     (org-element-property :contents-end elem))
                    "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))))

(ert-deftest org-gcal-test--update-existing-entry-cancelled ()
  "Verify that an existing headline is populated correctly from a cancelled
  calendar event object."
  (let (
        (org-todo-keywords '((sequence "TODO" "|" "DONE" "CANCELLED")))
        (org-gcal-cancelled-todo-keyword "CANCELLED")
        (org-gcal-remove-api-cancelled-events nil)
        (buf "\
* Old event summary
:PROPERTIES:
:ETag:     \"9999\"
:LOCATION: Somewhere else
:link: [[https://yahoo.com][Yahoo!]]
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<9999-10-06 Sun 17:00-21:00>

Old event description
:END:
"))
    (let ((org-gcal-update-cancelled-events-with-todo t))
      (org-gcal-test--with-temp-buffer
       buf
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (org-back-to-heading)
       (let ((elem (org-element-at-point)))
         (should (equal (org-gcal-test--title-to-string elem)
                        "My event summary"))
         (should (equal (org-element-property :todo-keyword elem)
                        "CANCELLED"))
         (should (equal (org-element-property :ETAG elem)
                        "\"12344321\""))
         (should (equal (org-element-property :LOCATION elem)
                        "Foobar's desk"))
         (should (equal (org-element-property :LINK elem)
                        "[[https://google.com][Google]]"))
         (should (equal (org-element-property :CALENDAR-ID elem)
                        "foo@foobar.com"))
         (should (equal (org-element-property :ENTRY-ID elem)
                        "foobar1234/foo@foobar.com")))
       ;; Check contents of "org-gcal" drawer
       (re-search-forward ":org-gcal:")
       (let ((elem (org-element-at-point)))
         (should (equal (org-element-property :drawer-name elem)
                        "org-gcal"))
         (should (equal (buffer-substring-no-properties
                         (org-element-property :contents-begin elem)
                         (org-element-property :contents-end elem))
                        "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))))
    (let ((org-gcal-update-cancelled-events-with-todo nil))
      (org-gcal-test--with-temp-buffer
       buf
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (org-back-to-heading)
       (let ((elem (org-element-at-point)))
         (should (equal (org-gcal-test--title-to-string elem)
                        "My event summary"))
         (should (equal (org-element-property :todo-keyword elem)
                        nil))
         (should (equal (org-element-property :ETAG elem)
                        "\"12344321\""))
         (should (equal (org-element-property :LOCATION elem)
                        "Foobar's desk"))
         (should (equal (org-element-property :LINK elem)
                        "[[https://google.com][Google]]"))
         (should (equal (org-element-property :TRANSPARENCY elem)
                        "opaque"))
         (should (equal (org-element-property :CALENDAR-ID elem)
                        "foo@foobar.com"))
         (should (equal (org-element-property :ENTRY-ID elem)
                        "foobar1234/foo@foobar.com")))
       ;; Check contents of "org-gcal" drawer
       (re-search-forward ":org-gcal:")
       (let ((elem (org-element-at-point)))
         (should (equal (org-element-property :drawer-name elem)
                        "org-gcal"))
         (should (equal (buffer-substring-no-properties
                         (org-element-property :contents-begin elem)
                         (org-element-property :contents-end elem))
                        "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))))
    (let ((org-gcal-remove-api-cancelled-events t))
      (org-gcal-test--with-temp-buffer
       buf
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (should (equal (buffer-substring-no-properties
                       (point-min) (point-max))
                      ""))))))

(ert-deftest org-gcal-test--update-existing-entry-already-cancelled ()
  "Verify that an existing headline is modified correctly according to the \
  value of ‘org-gcal-remove-events-with-cancelled-todo’."
  (let (
        (org-todo-keywords '((sequence "TODO" "|" "DONE" "CANCELLED")))
        (org-gcal-cancelled-todo-keyword "CANCELLED")
        (org-gcal-remove-api-cancelled-events nil)
        (org-gcal-remove-events-with-cancelled-todo nil)
        (buf "\
* Old event summary
:PROPERTIES:
:ETag:     \"9999\"
:LOCATION: Somewhere else
:link: [[https://yahoo.com][Yahoo!]]
:TRANSPARENCY: transparent
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<9999-10-06 Sun 17:00-21:00>

Old event description
:END:
"))
    (let ((org-gcal-update-cancelled-events-with-todo t))
      (org-gcal-test--with-temp-buffer
       buf
       ;; First mark the event as cancelled.
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (org-back-to-heading)
       (let ((elem (org-element-at-point)))
         (should (equal (org-gcal-test--title-to-string elem)
                        "My event summary"))
         (should (equal (org-element-property :todo-keyword elem)
                        "CANCELLED"))
         (should (equal (org-element-property :ETAG elem)
                        "\"12344321\""))
         (should (equal (org-element-property :LOCATION elem)
                        "Foobar's desk"))
         (should (equal (org-element-property :LINK elem)
                        "[[https://google.com][Google]]"))
         (should (equal (org-element-property :TRANSPARENCY elem)
                        "opaque"))
         (should (equal (org-element-property :CALENDAR-ID elem)
                        "foo@foobar.com"))
         (should (equal (org-element-property :ENTRY-ID elem)
                        "foobar1234/foo@foobar.com")))
       ;; Check contents of "org-gcal" drawer
       (re-search-forward ":org-gcal:")
       (let ((elem (org-element-at-point)))
         (should (equal (org-element-property :drawer-name elem)
                        "org-gcal"))
         (should (equal (buffer-substring-no-properties
                         (org-element-property :contents-begin elem)
                         (org-element-property :contents-end elem))
                        "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))
       ;; Now check that the event isn’t removed when
       ;; ‘org-gcal-remove-events-with-cancelled-todo’ is nil.
       (setq org-gcal-remove-api-cancelled-events t
             org-gcal-remove-events-with-cancelled-todo nil)
       (org-back-to-heading)
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (org-back-to-heading)
       (let ((elem (org-element-at-point)))
         (should (equal (org-gcal-test--title-to-string elem)
                        "My event summary"))
         (should (equal (org-element-property :todo-keyword elem)
                        "CANCELLED"))
         (should (equal (org-element-property :ETAG elem)
                        "\"12344321\""))
         (should (equal (org-element-property :LOCATION elem)
                        "Foobar's desk"))
         (should (equal (org-element-property :LINK elem)
                        "[[https://google.com][Google]]"))
         (should (equal (org-element-property :TRANSPARENCY elem)
                        "opaque"))
         (should (equal (org-element-property :CALENDAR-ID elem)
                        "foo@foobar.com"))
         (should (equal (org-element-property :ENTRY-ID elem)
                        "foobar1234/foo@foobar.com")))
       ;; Check contents of "org-gcal" drawer
       (re-search-forward ":org-gcal:")
       (let ((elem (org-element-at-point)))
         (should (equal (org-element-property :drawer-name elem)
                        "org-gcal"))
         (should (equal (buffer-substring-no-properties
                         (org-element-property :contents-begin elem)
                         (org-element-property :contents-end elem))
                        "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))
       ;; Now check that the event is removed when
       ;; ‘org-gcal-remove-events-with-cancelled-todo’ is t.
       (setq org-gcal-remove-api-cancelled-events t
             org-gcal-remove-events-with-cancelled-todo t)
       (org-back-to-heading)
       (org-gcal--update-entry org-gcal-test-calendar-id
                               org-gcal-test-cancelled-event)
       (should (equal (buffer-substring-no-properties
                       (point-min) (point-max))
                      ""))))))

(ert-deftest org-gcal-test--update-existing-entry-scheduled ()
  "Same as ‘org-gcal-test--update-existing-entry’, but with SCHEDULED
property."
  (org-gcal-test--with-temp-buffer
   "\
* Old event summary
SCHEDULED: <9999-10-06 Sun 17:00-21:00>
:PROPERTIES:
:ETag:     \"9999\"
:LOCATION: Somewhere else
:link: [[https://yahoo.com][Yahoo!]]
:TRANSPARENCY: transparent
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
Old event description
:END:
"
   (org-gcal--update-entry org-gcal-test-calendar-id
                           org-gcal-test-event)
   (org-back-to-heading)
   (let ((elem (org-element-at-point)))
     (should (equal (org-element-property
                     :raw-value
                     (org-element-property :scheduled elem))
                    "<2019-10-06 Sun 17:00-21:00>"))
     (should (equal (org-gcal-test--title-to-string elem)
                    "My event summary"))
     (should (equal (org-element-property :ETAG elem)
                    "\"12344321\""))
     (should (equal (org-element-property :LOCATION elem)
                    "Foobar's desk"))
     (should (equal (org-element-property :LINK elem)
                    "[[https://google.com][Google]]"))
     (should (equal (org-element-property :TRANSPARENCY elem)
                    "opaque"))
     (should (equal (org-element-property :CALENDAR-ID elem)
                    "foo@foobar.com"))
     (should (equal (org-element-property :ENTRY-ID elem)
                    "foobar1234/foo@foobar.com")))
   ;; Check contents of "org-gcal" drawer
   (re-search-forward ":org-gcal:")
   (let ((elem (org-element-at-point)))
     (should (equal (org-element-property :drawer-name elem)
                    "org-gcal"))
     (should (equal (buffer-substring-no-properties
                     (org-element-property :contents-begin elem)
                     (org-element-property :contents-end elem))
                    "\
My event description

Second paragraph
")))))

(ert-deftest org-gcal-test--update-existing-entry-with-id ()
  "Verify that existing IDs in an existing headline will be preserved."
  (org-gcal-test--with-temp-buffer
   "\
* Old event summary
:PROPERTIES:
:LOCATION: Somewhere else
:link: [[https://yahoo.com][Yahoo!]]
:TRANSPARENCY: transparent
:calendar-id: foo@foobar.com
:entry-id:       ABCD-EFGH
:END:
:org-gcal:
<9999-10-06 Sun 17:00-21:00>

Old event description
:END:
"
   (org-gcal--update-entry org-gcal-test-calendar-id
                           org-gcal-test-event)
   (org-back-to-heading)
   (let ((elem (org-element-at-point)))
     (should (equal (org-gcal-test--title-to-string elem)
                    "My event summary"))
     (should (equal (org-element-property :ETAG elem)
                    "\"12344321\""))
     (should (equal (org-element-property :LOCATION elem)
                    "Foobar's desk"))
     (should (equal (org-element-property :LINK elem)
                    "[[https://google.com][Google]]"))
     (should (equal (org-element-property :TRANSPARENCY elem)
                    "opaque"))
     (should (equal (org-element-property :CALENDAR-ID elem)
                    "foo@foobar.com")))
   ;; The canonical ID should be that generated by org-gcal.
   (should (equal (org-gcal--all-property-local-values (point) org-gcal-entry-id-property nil)
                  '("foobar1234/foo@foobar.com")))
   (should (equal (org-entry-get (point) org-gcal-entry-id-property)
                  '"foobar1234/foo@foobar.com"))
   ;; Check contents of "org-gcal" drawer
   (re-search-forward ":org-gcal:")
   (let ((elem (org-element-at-point)))
     (should (equal (org-element-property :drawer-name elem)
                    "org-gcal"))
     (should (equal (buffer-substring-no-properties
                     (org-element-property :contents-begin elem)
                     (org-element-property :contents-end elem))
                    "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
")))))

(ert-deftest org-gcal-test--post-at-point-basic ()
  "Verify basic case of ‘org-gcal-post-to-point’."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * *))
    (let ((org-gcal-managed-post-at-point-update-existing 'always-push))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--sync-update-entries-stale-marker ()
  "Verify sync reports a clear error for entries with stale markers."
  (let ((errors nil))
    (cl-letf (((symbol-function 'deferred:loop)
               (lambda (entries body)
                 (funcall body (car entries))))
              ((symbol-function 'error)
               (lambda (format-string &rest args)
                 (let ((message (apply #'format format-string args)))
                   (push message errors)
                   (signal 'error (list message))))))
      (ignore-errors
        (deferred:sync!
         (org-gcal--sync-update-entries
          org-gcal-test-calendar-id
          (list
           (org-gcal--event-entry-create
            :entry-id "foobar1234/foo@foobar.com"
            :marker (org-gcal-test--stale-marker "* My event summary\n")
            :event org-gcal-test-event))
          t)))
      (should
       (cl-some
        (lambda (error)
          (string-match-p "marker.*buffer.*foobar1234/foo@foobar.com.*killed"
                          error))
        errors)))))

(ert-deftest org-gcal-test--with-point-at-no-widen-stale-marker ()
  "Verify stale markers report the killed buffer before moving point."
  (org-gcal-test--should-error-match "marker.*buffer has been killed"
    (org-gcal--with-point-at-no-widen
        (org-gcal-test--stale-marker "* My event summary\n")
      (point))))

(ert-deftest org-gcal-test--post-at-point-api-response ()
  "Verify that ‘org-gcal-post-to-point’ updates an event using the data
returned from the Google Calendar API."
  (org-gcal-test--with-temp-buffer
   "\
* Original summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Original location
:link: [[https://yahoo.com][Yahoo!]]
:TRANSPARENCY: transparent
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2021-03-05 Fri 12:00-14:00>

Original description

Original second paragraph
:END:
"
   (defvar update-entry-hook-called nil)
   (setq update-entry-hook-called nil)
   (let (org-gcal-after-update-entry-functions)
     (defun update-entry-hook (calendar-id event update-mode)
       (message "update-entry-hook %S %S %S" calendar-id event update-mode)
       (setq update-entry-hook-called t))
     (add-hook 'org-gcal-after-update-entry-functions #'update-entry-hook)
     (with-mock
      (stub org-gcal--time-zone => '(0 "UTC"))
      (stub org-generic-id-add-location => nil)
      (stub org-gcal--get-access-token => "my_access_token")
      (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
      (stub request-deferred =>
            (deferred:succeed
             (make-request-response
              :status-code 200
              :data org-gcal-test-event)))
      (let ((org-gcal-managed-post-at-point-update-existing 'always-push))
        (org-gcal-post-at-point)
        (org-back-to-heading)
        (should (equal update-entry-hook-called t))
        (let ((elem (org-element-at-point)))
          (should (equal (org-gcal-test--title-to-string elem)
                         "My event summary"))
          (should (equal (org-element-property :ETAG elem)
                         "\"12344321\""))
          (should (equal (org-element-property :LOCATION elem)
                         "Foobar's desk"))
          (should (equal (org-element-property :LINK elem)
                         "[[https://google.com][Google]]"))
          (should (equal (org-element-property :TRANSPARENCY elem)
                         "opaque"))
          (should (equal (org-element-property :CALENDAR-ID elem)
                         "foo@foobar.com"))
          (should (equal (org-element-property :ENTRY-ID elem)
                         "foobar1234/foo@foobar.com")))
        ;; Check contents of "org-gcal" drawer
        (re-search-forward ":org-gcal:")
        (let ((elem (org-element-at-point)))
          (should (equal (org-element-property :drawer-name elem)
                         "org-gcal"))
          (should (equal (buffer-substring-no-properties
                          (org-element-property :contents-begin elem)
                          (org-element-property :contents-end elem))
                         "\
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
"))))))))

(ert-deftest org-gcal-test--post-event-stale-marker ()
  "Verify that post-event callbacks report stale markers clearly."
  (let ((marker (org-gcal-test--stale-marker "* My event summary\n")))
    (cl-letf (((symbol-function 'org-gcal--get-access-token)
               (lambda (_calendar-id) "my_access_token"))
              ((symbol-function 'request-deferred)
               (lambda (&rest _args)
                 (deferred:succeed
                  (make-request-response
                   :status-code 200
                   :data org-gcal-test-event)))))
      (org-gcal-test--should-error-match "marker.*buffer has been killed"
        (deferred:sync!
         (org-gcal--post-event
          "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
          "My event summary" "Foobar's desk"
          '((url . "https://google.com") (title . "Google"))
          "My event description"
          org-gcal-test-calendar-id
          marker "opaque" "\"12344321\"" "foobar1234"))))))

(ert-deftest org-gcal-test--post-at-point-managed-update-existing-gcal ()
  "Verify ‘org-gcal-post-at-point’ with ‘org-gcal-managed-update-existing-mode’
set to \"gcal\"."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (y-or-n-p *) => nil)
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * t))
    (let ((org-gcal-managed-update-existing-mode "gcal"))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--post-at-point-managed-update-existing-org ()
  "Verify ‘org-gcal-post-at-point’ with ‘org-gcal-managed-update-existing-mode’
set to \"org\"."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * nil))
    (let ((org-gcal-managed-update-existing-mode "org"))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--post-at-point-managed-create-from-entry-gcal ()
  "Verify ‘org-gcal-post-at-point’ with ‘org-gcal-managed-create-from-entry-mode’
set to \"gcal\"."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (y-or-n-p *) => nil)
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                nil
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" nil
                                * * t))
    (let ((org-gcal-managed-update-existing-mode "gcal")
          (org-gcal-managed-create-from-entry-mode "gcal"))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--post-at-point-managed-create-from-entry-org ()
  "Verify ‘org-gcal-post-at-point’ with ‘org-gcal-managed-create-from-entry-mode’
set to \"org\"."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" nil
                                * * nil))
    (let ((org-gcal-managed-update-existing-mode "gcal")
          (org-gcal-managed-create-from-entry-mode "org"))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--post-at-point-old-id-property ()
  "Verify that \":ID:\" property is read for event ID by \
‘org-gcal-post-to-point’ only if ‘org-gcal-entry-id-property’ is not present."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:ID:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * *))
    (let ((org-gcal-managed-post-at-point-update-existing 'always-push))
      (org-gcal-post-at-point))))
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:ID:             hello-world
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * *))
    (let ((org-gcal-managed-post-at-point-update-existing 'always-push))
      (org-gcal-post-at-point)))))


(ert-deftest org-gcal-test--post-at-point-no-id ()
  "Verify that ‘org-gcal-post-to-point’ doesn't send an ID to Calendar API if
an org-gcal Calendar Event ID can't be retrieved from the current entry."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" nil nil
                                * * *))
    (org-gcal-post-at-point)))
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id: ABCD-EFGH
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-06T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" nil nil
                                * * *))
    (org-gcal-post-at-point))))

(ert-deftest org-gcal-test--post-at-point-no-properties ()
  "Verify that ‘org-gcal-post-to-point’ fills in entries with no relevant
org-gcal properties with sane default values."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
"
   (with-mock
    (stub completing-read => "foo@foobar.com")
    (stub org-read-date => (encode-time 0 0 17 6 10 2019 nil nil t))
    (stub read-from-minibuffer => "4:00")
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00+0000" "2019-10-06T21:00:00+0000"
                                "My event summary" nil
                                nil nil
                                "foo@foobar.com"
                                * "opaque" nil nil
                                * * *))
    (org-gcal-post-at-point)))
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:Effort: 2:00
:END:
:LOGBOOK:
CLOCK: [2019-06-06 Thu 17:00]--[2019-06-06 Thu 18:00] => 1:00
:END:
"
   (with-mock
    (stub completing-read => "foo@foobar.com")
    (stub org-read-date => (encode-time 0 0 17 6 10 2019 nil nil t))
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (cl-letf
        (((symbol-function #'read-from-minibuffer)
          (lambda (_p initial-contents) initial-contents)))
      (mock (org-gcal--post-event "2019-10-06T17:00:00+0000" "2019-10-06T18:00:00+0000"
                                  "My event summary" nil
                                  nil nil
                                  "foo@foobar.com"
                                  * "opaque" nil nil
                                  * * *))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--post-at-point-default-duration ()
  "Verify that missing end time uses ‘org-gcal-event-default-duration’."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
"
   (with-mock
    (stub completing-read => "foo@foobar.com")
    (stub org-read-date => (encode-time 0 0 17 6 10 2019 nil nil t))
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (let ((org-gcal-event-default-duration 30))
      (cl-letf (((symbol-function #'read-from-minibuffer)
                 (lambda (_prompt initial-contents)
                   (should (equal initial-contents "0:30"))
                   initial-contents)))
        (mock (org-gcal--post-event "2019-10-06T17:00:00+0000" "2019-10-06T17:30:00+0000"
                                    "My event summary" nil
                                    nil nil
                                    "foo@foobar.com"
                                    * "opaque" nil nil
                                    * * *))
        (org-gcal-post-at-point))))))

(ert-deftest org-gcal-test--post-at-point-default-duration-minimum ()
  "Verify ‘org-gcal-event-default-duration’ is a minimum duration."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:Effort: 2:00
:END:
:LOGBOOK:
CLOCK: [2019-06-06 Thu 17:00]--[2019-06-06 Thu 18:00] => 1:00
:END:
"
   (with-mock
    (stub completing-read => "foo@foobar.com")
    (stub org-read-date => (encode-time 0 0 17 6 10 2019 nil nil t))
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (let ((org-gcal-event-default-duration 90))
      (cl-letf (((symbol-function #'read-from-minibuffer)
                 (lambda (_prompt initial-contents)
                   (should (equal initial-contents "1:30"))
                   initial-contents)))
        (mock (org-gcal--post-event "2019-10-06T17:00:00+0000" "2019-10-06T18:30:00+0000"
                                    "My event summary" nil
                                    nil nil
                                    "foo@foobar.com"
                                    * "opaque" nil nil
                                    * * *))
        (org-gcal-post-at-point))))))

(ert-deftest org-gcal-test--post-at-point-etag-no-id ()
  "Verify that ‘org-gcal-post-to-point’ fails if an ETag is present but
an event ID is not."
  :expected-result :failed
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:LOCATION: Foobar's desk
:TRANSPARENCY: opaque
:ETag:     \"12344321\"
:calendar-id: foo@foobar.com
:END:
:org-gcal:
<2019-10-06 Sun 17:00-21:00>

My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (stub request-deferred => (deferred:succeed nil))
    (org-gcal-post-at-point))))

(ert-deftest org-gcal-test--post-at-point-time-date-range ()
  "Verify that entry with a time/date range for its timestamp is parsed by
‘org-gcal-post-to-point’ (see https://orgmode.org/manual/Timestamps.html)."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
SCHEDULED: <2019-10-06 Sun 17:00>--<2019-10-07 Mon 21:00>
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
My event description

Second paragraph
:END:
"
   (with-mock
    (stub org-gcal--time-zone => '(0 "UTC"))
    (stub org-generic-id-add-location => nil)
    (stub org-gcal--get-access-token => "my_access_token")
    (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
    (mock (org-gcal--post-event "2019-10-06T17:00:00Z" "2019-10-07T21:00:00Z"
                                "My event summary" "Foobar's desk"
                                `((url . "https://google.com") (title . "Google"))
                                "My event description\n\nSecond paragraph"
                                "foo@foobar.com"
                                * "opaque" "\"12344321\"" "foobar1234"
                                * * *))
    (let ((org-gcal-managed-post-at-point-update-existing 'always-push))
      (org-gcal-post-at-point)))))

(ert-deftest org-gcal-test--delete-at-point-delete-drawer ()
  "Verify that the org-gcal drawer is deleted by ‘org-gcal-delete-at-point’ if
and only if the event at the point is successfully deleted by the Google
Calendar API."
  (let ((org-gcal-remove-api-cancelled-events nil)
        (org-gcal-update-cancelled-events-with-todo nil)
        (buf "\
* My event summary
SCHEDULED: <2019-10-06 Sun 17:00>--<2019-10-07 Mon 21:00>
:PROPERTIES:
:ETag:     \"12344321\"
:LOCATION: Foobar's desk
:link: [[https://google.com][Google]]
:TRANSPARENCY: opaque
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
My event description

Second paragraph
:END:
"))
    (org-gcal-test--with-temp-buffer
     buf
     ;; Don’t delete drawer if we don’t receive 200.
     (with-mock
      (let ((deferred:debug t))
        (stub org-gcal--time-zone => '(0 "UTC")))
      (stub org-generic-id-add-location => nil)
      (stub org-gcal--get-access-token => "my_access_token")
      (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
      (stub y-or-n-p => t)
      (stub alert => t)
      (stub request-deferred =>
            (deferred:succeed
             (make-request-response
              :status-code 500
              :error-thrown '(error . nil))))
      (deferred:sync!
       (deferred:$
        (org-gcal-delete-at-point)
        (deferred:error it #'ignore)))
      (org-back-to-heading)
      (should (re-search-forward ":org-gcal:" nil 'noerror))))

    ;; Delete drawer if we do receive 200.
    (org-gcal-test--with-temp-buffer
     buf
     (with-mock
      (let ((deferred:debug t))
        (stub org-gcal--time-zone => '(0 "UTC"))
        (stub org-generic-id-add-location => nil)
        (stub org-gcal--get-access-token => "my_access_token")
        (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
        (stub y-or-n-p => t)
        (stub request-deferred =>
              (deferred:succeed
               (make-request-response
                :status-code 200)))
        (deferred:sync! (org-gcal-delete-at-point))
        (org-back-to-heading)
        (should-not (re-search-forward ":org-gcal:" nil 'noerror)))))

    ;; Delete the entire entry if configured to
    (org-gcal-test--with-temp-buffer
     buf
     (with-mock
      (let ((deferred:debug t)
            (org-gcal-remove-api-cancelled-events t))
        (stub org-gcal--time-zone => '(0 "UTC"))
        (stub org-gcal--get-access-token => "my_access_token")
        (stub org-gcal--refresh-token => (deferred:succeed "test_access_token"))
        (stub y-or-n-p => t)
        (stub request-deferred =>
              (deferred:succeed
               (make-request-response
                :status-code 200)))
        (deferred:sync! (org-gcal-delete-at-point))
        (should (equal (buffer-string) "")))))))

(ert-deftest org-gcal-test--delete-event-stale-marker-on-etag-conflict ()
  "Verify delete-event reports stale markers clearly after HTTP 412."
  (let ((marker (org-gcal-test--stale-marker "* My event summary\n")))
    (cl-letf (((symbol-function 'org-gcal--get-access-token)
               (lambda (_calendar-id) "my_access_token"))
              ((symbol-function 'org-gcal--notify) #'ignore)
              ((symbol-function 'request-deferred)
               (lambda (&rest _args)
                 (deferred:succeed
                  (make-request-response
                   :status-code 412))))
              ((symbol-function 'org-gcal--get-event)
               (lambda (_calendar-id _event-id)
                 (deferred:succeed
                  (make-request-response
                   :status-code 200
                   :data org-gcal-test-event)))))
      (org-gcal-test--should-error-match "marker.*buffer has been killed"
        (deferred:sync!
         (org-gcal--delete-event org-gcal-test-calendar-id
                                 "foobar1234"
                                 "\"12344321\""
                                 marker))))))

(ert-deftest org-gcal-test--delete-at-point-stale-marker-in-finally ()
  "Verify delete-at-point reports clearly if its source buffer is killed."
  (org-gcal-test--with-temp-buffer
   "\
* My event summary
:PROPERTIES:
:ETag:     \"12344321\"
:calendar-id: foo@foobar.com
:entry-id:       foobar1234/foo@foobar.com
:END:
:org-gcal:
My event description
:END:
"
   (cl-letf (((symbol-function 'org-gcal--get-access-token)
              (lambda (_calendar-id) "my_access_token"))
             ((symbol-function 'y-or-n-p)
              (lambda (&rest _args) t))
             ((symbol-function 'org-gcal--delete-event)
              (lambda (_calendar-id _event-id _etag marker &optional _a-token)
                (kill-buffer (marker-buffer marker))
                (deferred:succeed nil)))
             ((symbol-function 'error)
              (lambda (format-string &rest args)
                (let ((message (apply #'format format-string args)))
                  (when (string-match-p "marker.*buffer has been killed" message)
                    (throw 'stale-marker-error message))
                  (signal 'error (list message))))))
     (should
      (string-match-p "marker.*buffer has been killed"
                      (catch 'stale-marker-error
                        (deferred:sync! (org-gcal-delete-at-point))
                        nil))))))


(ert-deftest org-gcal-test--save-with-full-day-event ()
  "Verify that a full day event will get set correctly."
  (org-gcal-test--with-temp-buffer
   "* "
   (org-gcal--update-entry org-gcal-test-calendar-id
                           org-gcal-test-full-day-event)
   (org-back-to-heading)
   ;; Check contents of "org-gcal" drawer
   (re-search-forward ":org-gcal:")
   (let ((elem (org-element-at-point)))
     (should (equal (buffer-substring-no-properties
                     (org-element-property :contents-begin elem)
                     (org-element-property :contents-end elem))
                    "\
<2019-10-06 Sun>

My event description

Second paragraph
")))))

(ert-deftest org-gcal-test--save-with-full-day-event-and-local-timezone ()
  "Verify that a full day event will get set correctly when local-timezone is set."
  (let (
        (org-gcal-local-timezone "Europe/London"))
    (org-gcal-test--with-temp-buffer
     "* "
     (org-gcal--update-entry org-gcal-test-calendar-id
                             org-gcal-test-full-day-event)
     (org-back-to-heading)
     ;; Check contents of "org-gcal" drawer
     (re-search-forward ":org-gcal:")
     (let ((elem (org-element-at-point)))
       (should (equal (buffer-substring-no-properties
                       (org-element-property :contents-begin elem)
                       (org-element-property :contents-end elem))
                      "\
<2019-10-06 Sun>

My event description

Second paragraph
"))))))

(ert-deftest org-gcal-test--ert-fail ()
  "Test handling of ERT failures in deferred code. Should fail."
  :expected-result :failed
  (with-mock
   (stub request-deferred =>
         (deferred:$
          (deferred:succeed
           (ert-fail "Failure"))
          (deferred:nextc it
                          (lambda (_)
                            (deferred:succeed "Success")))))
   (should (equal
            (deferred:sync! (request-deferred))
            "Success"))))

(ert-deftest org-gcal-test--convert-time-to-local-timezone()
  (should (equal
           (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-00:00" nil)
           "2021-03-03T11:30:00-00:00"))
  (should (equal
           (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-00:00" "")
           "2021-03-03T11:30:00+0000"))
  (should (equal
           (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "")
           "2021-03-03T19:30:00+0000"))
  (should (equal
           (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "Europe/London")
           "2021-03-03T19:30:00+0000")))
;; FIXME: Passed in local with Emacs 26.3 and 27.1, Failed in GitHub CI
;; (should (equal
;;          (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "Europe/Oslo")
;;          "2021-03-03T20:30:00+0100"))
;; (should (equal
;;          (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "America/New_York")
;;          "2021-03-03T14:30:00-0500"))
;; (should (equal
;;          (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "America/Los_Angeles")
;;          "2021-03-03T11:30:00-0800"))
;; (should (equal
;;          (org-gcal--convert-time-to-local-timezone "2021-03-03T11:30:00-08:00" "Asia/Shanghai")
;;          "2021-03-04T03:30:00+0800"))


;; TODO: fails with Cask, but succeeds interactively due to macro compilation issues.
;; (ert-deftest org-gcal-test--headline-archive-old-event ()
;;   "Check that `org-gcal--archive-old-event' parses headlines correctly.
;; Regression test for https://github.com/kidd/org-gcal.el/issues/172 .

;; Also tests that the `org-gcal--archive-old-event' function does
;; not loop over and over, archiving the same entry because it is
;; under another heading in the same file."
;;   (let ((org-archive-location "::* Archived")  ; Make the archive this same buffer
;;         (test-time "2022-01-30 Sun 01:23")
;;         (buf "\
;; #+CATEGORY: Test

;; * Event Title
;; :PROPERTIES:
;; :org-gcal-managed: something
;; :END:
;; <2021-01-01 Fri 12:34-14:35>
;; "))
;;     (org-test-with-temp-text-in-file
;;         buf
;;       (org-test-at-time (format "<%s>" test-time)
;;         ;; Ensure property drawer is not indented
;;         (setq-local org-adapt-indentation nil)
;;         (let* ((target-buf (format "\
;; #+CATEGORY: Test

;; * Archived

;; ** Event Title
;; :PROPERTIES:
;; :org-gcal-managed: something
;; :ARCHIVE_TIME: %s
;; :ARCHIVE_FILE: %s
;; :ARCHIVE_CATEGORY: Test
;; :END:
;; <2021-01-01 Fri 12:34-14:35>
;; "
;;                                    test-time
;;                                         ; The variable `file' is the current file
;;                                         ; name under the macro
;;                                         ; `org-test-with-temp-text-in-file'
;;                                    file)))
;;           (require 'org-archive)
;;           (org-gcal--archive-old-event)
;;           (let ((bufstr
;;                  (buffer-substring-no-properties (point-min) (point-max))))
;;             (should (string-equal bufstr target-buf))))))))

(ert-deftest org-gcal-test--strip-html ()
  "Verify that `org-gcal--strip-html' converts HTML to plain text."
  (should (equal (org-gcal--strip-html
                  "<html-blob>Click the link:<br><a href=\"https://meet.jit.si/Weekly\">https://meet.jit.si/Weekly</a><br></html-blob>")
                 "Click the link:\nhttps://meet.jit.si/Weekly"))
  ;; List items
  (should (equal (org-gcal--strip-html "<ul><li>First</li><li>Second</li></ul>")
                 "- First\n- Second"))
  ;; HTML entities
  (should (equal (org-gcal--strip-html "foo &amp; bar &lt;baz&gt; &quot;quux&quot; &#39;x&#39; &nbsp;")
                 "foo & bar <baz> \"quux\" 'x'"))
  ;; Consecutive blank lines collapsed
  (should (equal (org-gcal--strip-html "a<br><br><br><br>b")
                 "a\n\nb"))
  ;; Plain text passes through
  (should (equal (org-gcal--strip-html "No HTML here")
                 "No HTML here")))

(ert-deftest org-gcal-test--strip-html-p ()
  "Verify that `org-gcal--strip-html-p' respects global default and overrides."
  ;; Global default nil
  (let ((org-gcal-strip-html-descriptions nil)
        (org-gcal-strip-html-descriptions-overrides nil))
    (should-not (org-gcal--strip-html-p "cal@example.com")))
  ;; Global default t
  (let ((org-gcal-strip-html-descriptions t)
        (org-gcal-strip-html-descriptions-overrides nil))
    (should (org-gcal--strip-html-p "cal@example.com")))
  ;; Per-calendar override enables stripping despite global nil
  (let ((org-gcal-strip-html-descriptions nil)
        (org-gcal-strip-html-descriptions-overrides
         '(("cal@example.com" . t))))
    (should (org-gcal--strip-html-p "cal@example.com"))
    (should-not (org-gcal--strip-html-p "other@example.com")))
  ;; Per-calendar override disables stripping despite global t
  (let ((org-gcal-strip-html-descriptions t)
        (org-gcal-strip-html-descriptions-overrides
         '(("shared@group.calendar.google.com" . nil))))
    (should-not (org-gcal--strip-html-p "shared@group.calendar.google.com"))
    (should (org-gcal--strip-html-p "personal@gmail.com"))))

(ert-deftest org-gcal-test--update-entry-strip-html ()
  "Verify that `org-gcal--update-entry' strips HTML when enabled."
  ;; With stripping enabled, HTML is converted to plain text.
  (let ((org-gcal-strip-html-descriptions t)
        (org-gcal-strip-html-descriptions-overrides nil))
    (org-gcal-test--with-temp-buffer
        "* "
      (org-gcal--update-entry org-gcal-test-calendar-id
                              org-gcal-test-html-event)
      (org-back-to-heading)
      (re-search-forward ":org-gcal:")
      (let* ((elem (org-element-at-point))
             (contents (buffer-substring-no-properties
                        (org-element-property :contents-begin elem)
                        (org-element-property :contents-end elem))))
        ;; Should not contain HTML tags (use regex that won't match Org timestamps)
        (should-not (string-match-p "</?[a-zA-Z][^>]*>" contents))
        ;; Should contain converted text
        (should (string-match-p "Click the link:" contents))
        (should (string-match-p "https://meet.jit.si/Weekly" contents))
        ;; Entity decoded
        (should (string-match-p "& more info" contents))))))

(ert-deftest org-gcal-test--update-entry-preserve-html ()
  "Verify that `org-gcal--update-entry' preserves HTML when stripping disabled."
  ;; With stripping disabled (default), HTML is preserved.
  (let ((org-gcal-strip-html-descriptions nil)
        (org-gcal-strip-html-descriptions-overrides nil))
    (org-gcal-test--with-temp-buffer
        "* "
      (org-gcal--update-entry org-gcal-test-calendar-id
                              org-gcal-test-html-event)
      (org-back-to-heading)
      (re-search-forward ":org-gcal:")
      (let* ((elem (org-element-at-point))
             (contents (buffer-substring-no-properties
                        (org-element-property :contents-begin elem)
                        (org-element-property :contents-end elem))))
        ;; Should still contain HTML tags
        (should (string-match-p "<html-blob>" contents))
        (should (string-match-p "</a>" contents))
        ;; Entity NOT decoded
        (should (string-match-p "&amp;" contents))))))

(ert-deftest org-gcal-test--update-entry-preserve-html-per-calendar ()
  "Verify per-calendar override preserves HTML in `org-gcal--update-entry'."
  ;; Global default is t, but this specific calendar has stripping disabled.
  (let ((org-gcal-strip-html-descriptions t)
        (org-gcal-strip-html-descriptions-overrides
         `((,org-gcal-test-calendar-id . nil))))
    (org-gcal-test--with-temp-buffer
        "* "
      (org-gcal--update-entry org-gcal-test-calendar-id
                              org-gcal-test-html-event)
      (org-back-to-heading)
      (re-search-forward ":org-gcal:")
      (let* ((elem (org-element-at-point))
             (contents (buffer-substring-no-properties
                        (org-element-property :contents-begin elem)
                        (org-element-property :contents-end elem))))
        (should (string-match-p "<html-blob>" contents))
        (should (string-match-p "</a>" contents))
        (should (string-match-p "&amp;" contents))))))

(ert-deftest org-gcal-test--update-entry-strip-html-per-calendar ()
  "Verify per-calendar override for HTML stripping in `org-gcal--update-entry'."
  ;; Global default is nil, but this specific calendar has stripping enabled.
  (let ((org-gcal-strip-html-descriptions nil)
        (org-gcal-strip-html-descriptions-overrides
         `((,org-gcal-test-calendar-id . t))))
    (org-gcal-test--with-temp-buffer
        "* "
      (org-gcal--update-entry org-gcal-test-calendar-id
                              org-gcal-test-html-event)
      (org-back-to-heading)
      (re-search-forward ":org-gcal:")
      (let* ((elem (org-element-at-point))
             (contents (buffer-substring-no-properties
                        (org-element-property :contents-begin elem)
                        (org-element-property :contents-end elem))))
        (should-not (string-match-p "</?[a-zA-Z][^>]*>" contents))
        (should (string-match-p "& more info" contents))))))

;;; TODO: Figure out mocking for POST/PATCH followed by GET
;;; - ‘mock‘ might work for this - the argument list must be specified up
;;;   front, but the wildcard ‘*’ can be used to match any value. If that
;;;   doesn’t work, use ‘cl-flet’.

;;; TODO: Figure out how to set up org-id for mocking (org-mode tests should help?)
;;; - There are actually no org-mode tests for this.
;;; - Set ‘org-id-locations’ (a hash table). This maps each ID to the file in
;;;   which the ID is found, so a temp file (not just a temp buffer) is needed.
